//===================================================================================
//
//  Copyright (C) 2019 Alex Lotz, Dennis Stampfer, Matthias Lutz
//
//        lotz@hs-ulm.de
//        stampfer@hs-ulm.de
//        lutz@hs-ulm.de
//
//        Servicerobotik Ulm
//        Christian Schlegel
//        Ulm University of Applied Sciences
//        Prittwitzstr. 10
//        89075 Ulm
//        Germany
//
//  This file is part of the SmartMDSD Toolchain V3. 
//
//===================================================================================
grammar org.xtext.base.docuterminals.DocuTerminals with org.eclipse.xtext.common.Terminals

import "http://www.ecore.org/base/documentation" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

AbstractDocumentedElement:
	{AbstractDocumentedElement}
	// add this line as first element in your ParserRule(s) to implement the AbstractDocumentedElement base class
	(documentation=DOCU_COMMENT)?
;

// we override the multiline comments to explicitly distinguish between regular comments starting with "/*" and the new docu-comments starting with "/**"
@Override
terminal ML_COMMENT : '/*' (!'*') -> '*/' ;
terminal fragment ML_DOCUMENTATION: '/**' -> '*/' ;

// we override the single-line comments to explicitly distinguish between regular comments 
// starting with two slashes "//" and the new docu-comments starting with three slashes "///"
@Override
terminal SL_COMMENT : '//' (!'/') !('\n'|'\r')* ('\r'? '\n')?;
terminal fragment SL_DOCUMENTATION: '///' !('\n'|'\r')* ('\r'? '\n')?;

// documentation comments can be either single-line (starting with "///") or multi-line (starting with "/**" and ending with "*/")
terminal DOCU_COMMENT : ML_DOCUMENTATION | SL_DOCUMENTATION;
