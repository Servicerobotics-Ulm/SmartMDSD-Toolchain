/********************************************************************************
 * Copyright (c) 2017 Technische Hochschule Ulm, Servicerobotics Ulm, Germany
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 * 
 * SPDX-License-Identifier: EPL-2.0
 * 
 * Contributors:
 *   Alex Lotz, Dennis Stampfer, Matthias Lutz
 ********************************************************************************/
grammar org.eclipse.smartmdsd.xtext.system.targetPlatform.TargetPlatform with org.eclipse.smartmdsd.xtext.service.roboticMiddleware.RoboticMiddleware

import "http://www.eclipse.org/smartmdsd/system/targetPlatform" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

TargetPlatformModel returns TargetPlatformModel:
	'TargetPlatformModel' name=ID
	'{'
		elements+=AbstractTPElement*
	'}';

AbstractTPElement returns AbstractTPElement:
	TargetPlatformDefinition | NetworkConnection;
	
NetworkConnection returns NetworkConnection:
	'NetworkConnection'
	'{'
		endpoint1=[NetworkInterface|FQN] '<->' endpoint2=[NetworkInterface|FQN]
		('kind' kind=EString)? 
	'}';

TargetPlatformDefinition returns TargetPlatformDefinition:
	'TargetPlatformDefinition' name=ID
	'{'
		(os=OperatingSystem)?
		elements+=AbstractTPSubNode*
	'}';

OperatingSystem returns OperatingSystem: 
	Windows | Linux | MacOS
;

Windows returns Windows:
	{Windows}
	'Windows' (kind=STRING)? (';')?
;
Linux returns Linux:
	{Linux}
	'Linux' (kind=STRING)? (';')?
;
MacOS returns MacOS:
	{MacOS}
	'MacOS' (kind=STRING)? (';')?
;


AbstractTPSubNode returns AbstractTPSubNode: 
	NetworkInterface | CPU | LoginAccount | TargetMiddleware
;


NetworkInterface returns NetworkInterface:
	'NetworkInterface' name=ID '{'
		(
			('HostAddress' hostAddress=STRING (':' portNr=EInt)?) &			
			('Kind' kind=EString)?
		)
	'}'
;

CPU returns CPU:
	'CPU' name=ID
	'{'
		(
			('Kind' kind=EString)? &
			('CoresTally' coresTally=EInt)
		)
	'}'
;
	

LoginAccount returns LoginAccount:
	'LoginAccount' name=ID '{'
		(
			('FullName' fullname=STRING)? &
			('Email' email=STRING)?
		)
	'}'
;

TargetMiddleware returns TargetMiddleware: 
	'TargetMiddleware' middleware=RoboticMiddleware (';')?
;

