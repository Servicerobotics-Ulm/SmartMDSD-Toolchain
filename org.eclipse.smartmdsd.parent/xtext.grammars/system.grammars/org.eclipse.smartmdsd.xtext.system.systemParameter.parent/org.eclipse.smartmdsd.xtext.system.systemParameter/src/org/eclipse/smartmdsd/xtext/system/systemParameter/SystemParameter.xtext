/********************************************************************************
 * Copyright (c) 2017 Technische Hochschule Ulm, Servicerobotics Ulm, Germany
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 * 
 * SPDX-License-Identifier: EPL-2.0
 * 
 * Contributors:
 *   Alex Lotz, Dennis Stampfer, Matthias Lutz
 ********************************************************************************/
grammar org.eclipse.smartmdsd.xtext.system.systemParameter.SystemParameter with org.eclipse.smartmdsd.xtext.base.basicAttributes.BasicAttributes

import "http://www.eclipse.org/smartmdsd/system/systemParameter" as systemParameter
import "http://www.eclipse.org/smartmdsd/system/componentArchitecture" as componentArchitecture
import "http://www.eclipse.org/smartmdsd/component/componentParameter" as componentParameter
import "http://www.eclipse.org/smartmdsd/base/basicAttributes" as basicAttributes
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

SystemParamModel returns systemParameter::SystemParamModel:
	{systemParameter::SystemParamModel}
	'SystemParameterModel' name=ID 'extendsSystem' system=[componentArchitecture::SystemComponentArchitecture|FQN] '{'
		components+=ComponentParameterInstance*
	'}'
;

ComponentParameterInstance returns systemParameter::ComponentParameterInstance:
	'ComponentParameterInstance' componentInstance=[componentArchitecture::ComponentInstance|FQN] ('refinesParamStruct' componentParam=[componentParameter::ComponentParameter|FQN])?
	'{'
		parameters+=ParameterRefinement*
	'}'
;
	
ParameterRefinement returns systemParameter::ParameterRefinement:
	(documentation=DOCU_COMMENT)?
	'ParameterRefinement' parameter=[componentParameter::ComponentParameterBase|FQN]
	'{'
		attributes+=AttributeRefinement*
	'}'
;
