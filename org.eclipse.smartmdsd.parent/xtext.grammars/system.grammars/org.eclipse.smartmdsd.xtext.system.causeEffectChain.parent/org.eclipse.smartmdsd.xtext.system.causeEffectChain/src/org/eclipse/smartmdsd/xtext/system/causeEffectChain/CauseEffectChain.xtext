//================================================================
//
//  Copyright (C) 2017 Alex Lotz, Dennis Stampfer, Matthias Lutz
//
//        lotz@hs-ulm.de
//        stampfer@hs-ulm.de
//        lutz@hs-ulm.de
//
//        Servicerobotik Ulm
//        Christian Schlegel
//        Ulm University of Applied Sciences
//        Prittwitzstr. 10
//        89075 Ulm
//        Germany
//
//  This file is part of the SmartMDSD Toolchain V3. 
//
//================================================================
grammar org.eclipse.smartmdsd.xtext.system.causeEffectChain.CauseEffectChain with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/smartmdsd/system/causeEffectChain" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/smartmdsd/system/activityArchitecture" as activityArchitecture

CuaseEffectChainModel returns CuaseEffectChainModel:
	'CuaseEffectChainModel'	name=ID 'refinesActivityArch' actArch=[activityArchitecture::ActivityArchitectureModel|FQN]
	'{'
		chains+=ActivityChain*
	'}';

EInt returns ecore::EInt:
	'-'? INT;
FQN:
	ID ('.' ID)*
;


ActivityChain returns ActivityChain:
	{ActivityChain}
	'ActivityChain'	name=ID
	'{'
		('chainLinks' '{' chainLinks+=AbstractChainLink ( "->" chainLinks+=AbstractChainLink)* '}' )?
		(
			(desiredMinResponse=MinResponseTime)? &
			(desiredMaxResponse=MaxResponseTime)?
		)
	'}';

AbstractChainLink returns AbstractChainLink:
	ActivityLink | InputHandlerLink;

ActivityLink returns ActivityLink:
	'act.' ref=[activityArchitecture::ActivityNode|FQN]
;

InputHandlerLink returns InputHandlerLink:
	'hnd.' ref=[activityArchitecture::InputHandlerNode|FQN]
;



MinResponseTime returns MinResponseTime:
	'desiredMinResponse' value=EInt unit=TimeUnit (';')?
;

MaxResponseTime returns MaxResponseTime:
	'desiredMaxResponse' value=EInt unit=TimeUnit (';')?
;
	
enum TimeUnit returns activityArchitecture::TimeUnit:
	SEC = 'sec' | MSEC = 'ms' | USEC = 'us';