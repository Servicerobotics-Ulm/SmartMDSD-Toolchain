/********************************************************************************
 * Copyright (c) 2019 Technische Hochschule Ulm, Servicerobotics Ulm, Germany
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 * 
 * SPDX-License-Identifier: EPL-2.0
 * 
 * Contributors:
 *   Alex Lotz, Dennis Stampfer, Matthias Lutz
 ********************************************************************************/
grammar org.eclipse.smartmdsd.xtext.base.docuterminals.DocuTerminals with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/smartmdsd/base/documentation" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore


AbstractDocumentationElement:
	{AbstractDocumentationElement}
	// add this line as first element in your ParserRule(s) to implement the AbstractDocumentedElement base class
	(documentation=DOCU_COMMENT)?
;

// we override the multiline comments to explicitly distinguish between regular comments starting with "/*" and the new docu-comments starting with "/**"
@Override
terminal ML_COMMENT : '/*' (!'*') -> '*/' ;
terminal fragment ML_DOCUMENTATION: '/**' -> '*/' ;

// we override the single-line comments to explicitly distinguish between regular comments 
// starting with two slashes "//" and the new docu-comments starting with three slashes "///"
@Override
terminal SL_COMMENT : '//' (!'/') !('\n'|'\r')* ('\r'? '\n')?;
terminal fragment SL_DOCUMENTATION: '///' !('\n'|'\r')* ('\r'? '\n')?;

// documentation comments can be either single-line (starting with "///") or multi-line (starting with "/**" and ending with "*/")
terminal DOCU_COMMENT : ML_DOCUMENTATION | SL_DOCUMENTATION;
