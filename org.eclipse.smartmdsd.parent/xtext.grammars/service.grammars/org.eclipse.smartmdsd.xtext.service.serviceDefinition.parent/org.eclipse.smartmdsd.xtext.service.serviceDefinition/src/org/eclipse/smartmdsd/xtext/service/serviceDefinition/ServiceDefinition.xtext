/********************************************************************************
 * Copyright (c) 2018 Technische Hochschule Ulm, Servicerobotics Ulm, Germany
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 * 
 * SPDX-License-Identifier: EPL-2.0
 * 
 * Contributors:
 *   Alex Lotz, Dennis Stampfer, Matthias Lutz
 ********************************************************************************/
grammar org.eclipse.smartmdsd.xtext.service.serviceDefinition.ServiceDefinition with org.eclipse.smartmdsd.xtext.base.docuterminals.DocuTerminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/smartmdsd/service/serviceDefinition" as service
import "http://www.eclipse.org/smartmdsd/service/communicationPattern" as commPattern
import "http://www.eclipse.org/smartmdsd/service/coordinationPattern" as coordPattern
import "http://www.eclipse.org/smartmdsd/service/communicationObject" as commObject
import "http://www.eclipse.org/smartmdsd/service/parameterDefinition" as param
import "http://www.eclipse.org/smartmdsd/service/componentMode" as modes
import "http://www.eclipse.org/smartmdsd/base/stateMachine" as stateMachine

ServiceDefModel returns service::ServiceDefModel:
	{service::ServiceDefModel}
	imports+=CommRepoImport*
	(repository=ServiceDefRepository)?
;

// parser rules for names
FQN returns ecore::EString: ID ('.' ID)* ;
FQNW returns ecore::EString: FQN '.*'? ;

EInt returns ecore::EInt:
	'-'? INT;
EString returns ecore::EString:
	STRING;


ServiceDefRepository returns service::ServiceDefRepository:
	(documentation=DOCU_COMMENT)?
	'ServiceDefRepository' name=ID ('version' version=ServiceRepoVersion)?
	'{'
		services+=AbstractServiceDefinition*
	'}';

ServiceRepoVersion returns service::ServiceRepoVersion:
	major=EInt '.' minor=EInt ('.' patch=EInt)?
;

CommRepoImport returns service::CommRepoImport:
	'#import' importedNamespace=FQNW (';')?
;


AbstractServiceDefinition returns service::AbstractServiceDefinition:
	CommunicationServiceDefinition | CoordinationServiceDefinition
;

CommunicationServiceDefinition returns service::CommunicationServiceDefinition:
	ForkingServiceDefinition | JoyningServiceDefinition | RequestAnswerServiceDefinition
;


ForkingServiceDefinition returns service::ForkingServiceDefinition:
	(documentation=DOCU_COMMENT)?
	'ForkingServiceDefinition' name=ID
	'{'
		pattern=ForkingPatternInstance
		properties+=ServiceProperty*
	'}'
;

JoyningServiceDefinition returns service::JoiningServiceDefinition: 
	(documentation=DOCU_COMMENT)?
	'JoiningServiceDefinition' name=ID
	'{'
		pattern=JoiningPatternInstance
		properties+=ServiceProperty*
	'}'
;


RequestAnswerServiceDefinition returns service::RequestAnswerServiceDefinition: 
	(documentation=DOCU_COMMENT)?
	'RequestAnswerServiceDefinition' name=ID
	'{'
		pattern=RequestAnswerPattern
		properties+=ServiceProperty*
	'}'
;



CoordinationServiceDefinition returns service::CoordinationServiceDefinition: 
	(documentation=DOCU_COMMENT)?
	'CoordinationServiceDefinition' name=ID
	'{'
		(
			(statePattern=StatePattern)? &
			(parameterPattern=ParameterPattern)? &
			(monitoringPattern=MonitoringPattern)? &
			(wiringPattern=DynamicWiringPattern)?
		)
		services+=CommunicationServiceUsage*
		properties+=ServiceProperty*
	'}'
;

CommunicationServiceUsage returns service::CommunicationServiceUsage:
	'CommunicationServiceUsage' name=ID 'uses' uses=[service::CommunicationServiceDefinition|FQN]
;

StatePattern returns coordPattern::StatePattern:
	{coordPattern::StatePattern}
	'StatePattern' '<' 
		'lifecycle' lifecycle=[stateMachine::StateMachine|FQN]
		('with' modes=[modes::ComponentModeCollection|FQN])?
	'>'
;

ParameterPattern returns coordPattern::ParameterPattern:
	{coordPattern::ParameterPattern} 
	'ParameterPattern' '<' parameterSet=[param::ParameterSetDefinition|FQN] '>'
;

DynamicWiringPattern returns coordPattern::DynamicWiringPattern: 
	{coordPattern::DynamicWiringPattern}
	'DynamicWiringPattern'
;

MonitoringPattern returns coordPattern::MonitoringPattern: 
	{coordPattern::MonitoringPattern}
	'MonitoringPattern'
	//TODO: this rule needs to be extended in future
;

ForkingPatternInstance returns commPattern::ForkingPattern:
	PushPattern | EventPattern 
;

JoiningPatternInstance returns commPattern::JoiningPattern:
	 SendPattern
;

RequestAnswerPattern returns commPattern::RequestAnswerPattern: 
	QueryPattern
;


PushPattern returns commPattern::PushPattern:
	{commPattern::PushPattern}
	'PushPattern' '<' 'DataType' '=' dataType=[commObject::CommunicationObject|FQN] '>'
;
EventPattern returns commPattern::EventPattern:
	{commPattern::EventPattern}
	'EventPattern' '<' 
		('EventType' '=' eventType=[commObject::CommunicationObject|FQN] & 
		'ActivationType' '=' activationType=[commObject::CommunicationObject|FQN] &
		'EventStateType' '=' eventStateType=[commObject::CommunicationObject|FQN]) 
	'>'
;
QueryPattern returns commPattern::QueryPattern:
	{commPattern::QueryPattern}
	'QueryPattern' '<' 
		('RequestType' '=' requestType=[commObject::CommunicationObject|FQN] & 
		'AnswerType' '=' answerType=[commObject::CommunicationObject|FQN])
	'>'
;
SendPattern returns commPattern::SendPattern:
	{commPattern::SendPattern}
	'SendPattern' '<' 'DataType' '=' dataType=[commObject::CommunicationObject|FQN] '>'
;

// this is just a placeholder for the later fully-fledged service-property definition
ServiceProperty returns service::ServiceProperty:
	'ServiceProperty' name=ID
	'{'
	//TODO: add property specification
	'}'
;
