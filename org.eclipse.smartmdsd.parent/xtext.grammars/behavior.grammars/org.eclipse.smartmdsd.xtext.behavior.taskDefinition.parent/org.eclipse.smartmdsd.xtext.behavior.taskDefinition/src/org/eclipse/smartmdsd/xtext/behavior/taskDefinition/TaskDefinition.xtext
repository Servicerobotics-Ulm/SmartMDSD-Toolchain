//================================================================
//
//  Copyright (C) 2017 Alex Lotz, Dennis Stampfer, Matthias Lutz
//
//        lotz@hs-ulm.de
//        stampfer@hs-ulm.de
//        lutz@hs-ulm.de
//
//        Servicerobotik Ulm
//        Christian Schlegel
//        Ulm University of Applied Sciences
//        Prittwitzstr. 10
//        89075 Ulm
//        Germany
//
//  This file is part of the SmartMDSD Toolchain V3. 
//
//================================================================
grammar org.eclipse.smartmdsd.xtext.behavior.taskDefinition.TaskDefinition with org.eclipse.smartmdsd.xtext.base.basicAttributes.BasicAttributes

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/smartmdsd/base/basicAttributes" as attr
import "http://www.eclipse.org/smartmdsd/behavior/taskDefinition" as task 

TaskDefinitionModel returns task::TaskDefinitionModel:
	{task::TaskDefinitionModel}
	(repository=TaskDefinitionRepository)?
;

TaskDefinitionRepository returns task::TaskDefinitionRepository:
	'TaskDefinitionRepository' name=ID
	'{'
		tasks+=TaskDefinition*
	'}'
;

TaskDefinition returns task::TaskDefinition:
	'TaskDefinition' name=ID
	'{'
		('in' '{' inAttribute+=AttributeDefinition* '}')?
		('out' '{' outAttribute+=AttributeDefinition* '}')?
		'results' '{' results+=TaskResult+ '}' 
	'}'
;

TaskResult returns task::TaskResult:
		result=TASK_RESULT_TYPES 'value' '=' resultValue=EString (';')?
;


enum TASK_RESULT_TYPES returns task::TASK_RESULT_TYPES:
	SUCCESS = 'SUCCESS' | ERROR = 'ERROR'
;